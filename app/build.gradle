apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.we.gallerymanager"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1200
        versionName "1.2.00"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            Properties properties = new Properties()
            InputStream inputStream = rootProject.file('local.properties').newDataInputStream()
            properties.load(inputStream)
            storeFile file(properties.getProperty('RELEASE_STOREFILE'))
            keyAlias properties.getProperty('RELEASE_KEY_ALIAS')
            keyPassword properties.getProperty('RELEASE_KEY_PASSWORD')
            storePassword properties.getProperty('RELEASE_STOREPASSWORD')
        }
    }


    buildTypes {
        release {
            manifestPlaceholders = [appName     : "@string/app_name"]
            minifyEnabled true
            zipAlignEnabled true // Zipalign优化
            shrinkResources true // 移除无用的resource文件
            debuggable false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            manifestPlaceholders = [appName     : "@string/app_name_debug"]
            minifyEnabled false
            zipAlignEnabled false // Zipalign优化
            shrinkResources false // 移除无用的resource文件
            debuggable true
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding = false
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }


    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def date = releaseTime()
            def applicationId = variant.applicationId
            def versionName = defaultConfig.versionName
            def versionCode = defaultConfig.versionCode
            def debugType = variant.buildType.name
            outputFileName = "${debugType}_${versionName}_${date}_${applicationId}.apk"
        }
    }
    dexOptions {
//        incremental true
        javaMaxHeapSize "4g"
    }
}

static def releaseTime() {
    return new Date().format("MMdd_hhmm", TimeZone.getTimeZone("Asia/Shanghai"))
}
//def qmui_arch_version = '2.0.0-alpha08'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":library")
    implementation project(":player")
    implementation project(":playerexo")
    implementation project(":dkvideocache")
    kapt "androidx.room:room-compiler:2.2.6" // For Kotlin use kapt instead of annotationProcessor
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

}
